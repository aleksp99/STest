#Область ОбработчикиСобытийФормы

&НаСервере
Процедура PA_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Элементы.ДеревоМетаданныхИмя.Видимость = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
&ИзменениеИКонтроль("ДобавитьНовыйУзелОбъектаМД")
Функция PA_ДобавитьНовыйУзелОбъектаМД(Знач ВеткаГруппы, Знач ТипМД, Знач ОбъектХДТО, Знач ИмяОбъектаВЕдЧисле, Знач Картинка)

	НовыйУзелДерева = ВеткаГруппы.ПолучитьЭлементы().Добавить();
	НовыйУзелДерева.ТипМД = ТипМД;

	#Удаление
	Если ТипЗнч(ОбъектХДТО[ИмяОбъектаВЕдЧисле].Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
	#КонецУдаления
	#Вставка
	Если ЕстьСиноним(ОбъектХДТО[ИмяОбъектаВЕдЧисле])
			И ТипЗнч(ОбъектХДТО[ИмяОбъектаВЕдЧисле].Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
	#КонецВставки
		Синоним = ОбъектХДТО[ИмяОбъектаВЕдЧисле].Properties.Synonym.item.content;
	ИначеЕсли ТипЗнч(ОбъектХДТО[ИмяОбъектаВЕдЧисле].Properties.Synonym) = Тип("СписокXDTO") Тогда
		Синоним = "";
		Для каждого ЭлементСписка Из ОбъектХДТО[ИмяОбъектаВЕдЧисле].Properties.Synonym.item Цикл
			Если ЭлементСписка.lang = "ru" Тогда
				Синоним = ЭлементСписка.content;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Синоним = "";
	КонецЕсли;

	НовыйУзелДерева.Имя      = ОбъектХДТО[ИмяОбъектаВЕдЧисле].Properties.Name;
	НовыйУзелДерева.Синоним  = Синоним;
	НовыйУзелДерева.ИД       = ОбъектХДТО[ИмяОбъектаВЕдЧисле].uuid;
	НовыйУзелДерева.Картинка = Картинка;

	Возврат НовыйУзелДерева;

КонецФункции

&НаКлиенте
&ИзменениеИКонтроль("ПостроитьСтруктуруФорм")
Процедура PA_ПостроитьСтруктуруФорм(СтруктураСвойств, Знач КаталогФорм, Знач ФабрикаПакета)

	МассивВФ = НайтиФайлы(КаталогФорм, "*.xml", Ложь);

	СтруктураФорм = Новый Структура;

	Для каждого ФайлОписания Из МассивВФ Цикл

		ОбъектХДТО = ПолучитьОбъектXDTO(ФайлОписания.ПолноеИмя, ФабрикаПакета);
		Если ОбъектХДТО = Неопределено Тогда
			Возврат;
		КонецЕсли;

		СтруктураФормы = Новый Структура;

		#Удаление
		Если ТипЗнч(ОбъектХДТО.Form.Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
			Попытка
				Синоним = ОбъектХДТО.Form.Properties.Synonym.item.content;
			Исключение
				Синоним = "";
			КонецПопытки;
		Иначе
			Синоним = "";
			Для каждого ЭлементСписка Из ОбъектХДТО.Form.Properties.Synonym.item Цикл
				Если ЭлементСписка.lang = "ru" Тогда
					Синоним = ЭлементСписка.content;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		#КонецУдаления
		#Вставка
		Синоним = СинонимОбъекта(ОбъектХДТО.Form);
		#КонецВставки

		Имя     = ОбъектХДТО.Form.Properties.Name;
		СтруктураФормы.Вставить("Синоним"      , Синоним);
		СтруктураФормы.Вставить("Идентификатор", ОбъектХДТО.Form.uuid);

		КаталогФормы = КаталогФорм
		+ РазделительПути
		+ ФайлОписания.ИмяБезРасширения
		+ РазделительПути
		+ "Ext"
		+ РазделительПути;

		ДополнитьСвойстваДаджестамиФормы(СтруктураФормы, КаталогФормы);

		СтруктураФорм.Вставить(Имя, СтруктураФормы);

	КонецЦикла;

	Если СтруктураФорм.Количество() > 0 Тогда
		СтруктураСвойств.Вставить("Forms", СтруктураФорм);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ПостроитьСтруктуруФормДляОбщегоКаталога")
Процедура PA_ПостроитьСтруктуруФормДляОбщегоКаталога(СтруктураСвойств, Знач МаскаФайловФорм, Знач ФабрикаПакета)

	МассивВФ = НайтиФайлы(КаталогХраненияФайловКонфигурации, МаскаФайловФорм + "*.xml", Ложь);

	СтруктураФорм = Новый Структура;

	Для каждого ФайлОписания Из МассивВФ Цикл

		// Игнор лишних файлов
		ИмяФайлаФормы = ФайлОписания.ИмяБезРасширения;
		ИмяФайлаФормы = Сред(ИмяФайлаФормы, СтрДлина(МаскаФайловФорм) +1);
		Если Найти(ИмяФайлаФормы, ".") <> 0 Тогда
			Продолжить;
		КонецЕсли;

		ОбъектХДТО = ПолучитьОбъектXDTO(ФайлОписания.ПолноеИмя, ФабрикаПакета);
		Если ОбъектХДТО = Неопределено Тогда
			Возврат;
		КонецЕсли;

		СтруктураФормы = Новый Структура;

		#Удаление
		Если ТипЗнч(ОбъектХДТО.Form.Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
			Попытка
				Синоним = ОбъектХДТО.Form.Properties.Synonym.item.content;
			Исключение
				Синоним = "";
			КонецПопытки;
		Иначе
			Синоним = "";
			Для каждого ЭлементСписка Из ОбъектХДТО.Form.Properties.Synonym.item Цикл
				Если ЭлементСписка.lang = "ru" Тогда
					Синоним = ЭлементСписка.content;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		#КонецУдаления
		#Вставка
		Синоним = СинонимОбъекта(ОбъектХДТО.Form);
		#КонецВставки

		Имя     = ОбъектХДТО.Form.Properties.Name;
		СтруктураФормы.Вставить("Синоним"      , Синоним);
		СтруктураФормы.Вставить("Идентификатор", ОбъектХДТО.Form.uuid);

		ПрефиксФормы = ФайлОписания.ИмяБезРасширения
		+ ".";

		ДополнитьСвойстваДаджестамиФормыИзОдногоКаталога(СтруктураФормы, ПрефиксФормы);

		СтруктураФорм.Вставить(Имя, СтруктураФормы);

	КонецЦикла;

	Если СтруктураФорм.Количество() > 0 Тогда
		СтруктураСвойств.Вставить("Forms", СтруктураФорм);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ПостроитьСтруктуруМакетов")
Процедура PA_ПостроитьСтруктуруМакетов(СтруктураСвойств, Знач КаталогМакетов, Знач ФабрикаПакета)

	МассивВФ = НайтиФайлы(КаталогМакетов, "*.xml", Ложь);

	СтруктураМакетов = Новый Структура;

	Для каждого ФайлОписания Из МассивВФ Цикл

		ОбъектХДТО = ПолучитьОбъектXDTO(ФайлОписания.ПолноеИмя, ФабрикаПакета);
		Если ОбъектХДТО = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтруктураМакета = Новый Структура;

		#Удаление
		Если ТипЗнч(ОбъектХДТО.Template.Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
			Попытка
				Синоним = ОбъектХДТО.Template.Properties.Synonym.item.content;
			Исключение
				Синоним = "";
			КонецПопытки;
		Иначе
			Синоним = "";
			Для каждого ЭлементСписка Из ОбъектХДТО.Template.Properties.Synonym.item Цикл
				Если ЭлементСписка.lang = "ru" Тогда
					Синоним = ЭлементСписка.content;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		#КонецУдаления
		#Вставка
			Синоним = СинонимОбъекта(ОбъектХДТО.Template);
		#КонецВставки

		Имя     = ОбъектХДТО.Template.Properties.Name;
		СтруктураМакета.Вставить("Синоним"      , Синоним);
		СтруктураМакета.Вставить("Идентификатор", ОбъектХДТО.Template.uuid);

		СтруктураМакета.Вставить("TemplateType" , ОбъектХДТО.Template.Properties.TemplateType);

		// Модуль объекта
		ИмяКаталогаШаблона = КаталогМакетов
		+ РазделительПути
		+ ФайлОписания.ИмяБезРасширения
		+ РазделительПути
		+ "Ext"
		+ РазделительПути;

		СтруктураМакета.Вставить("ХэшМодуля", ПолучитьХэшСуммуМакета(ИмяКаталогаШаблона));

		СтруктураМакетов.Вставить(Имя, СтруктураМакета);

	КонецЦикла;

	Если СтруктураМакетов.Количество() > 0 Тогда
		СтруктураСвойств.Вставить("Templates", СтруктураМакетов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ПостроитьСтруктуруМакетовИзОдногоКаталога")
Процедура PA_ПостроитьСтруктуруМакетовИзОдногоКаталога(СтруктураСвойств, Знач Префикс, Знач ФабрикаПакета)

	МассивВФ = НайтиФайлы(НормализованноеИмяКаталога(КаталогХраненияФайловКонфигурации), Префикс + "*.xml", Ложь);

	СтруктураМакетов = Новый Структура;

	Для каждого ФайлОписания Из МассивВФ Цикл
		// Игнор лишних файлов
		ИмяФайлаМакета = ФайлОписания.ИмяБезРасширения;
		ИмяФайлаМакета = Сред(ИмяФайлаМакета, СтрДлина(Префикс) +1);
		Если Найти(ИмяФайлаМакета, ".") <> 0 Тогда
			Продолжить;
		КонецЕсли;

		ОбъектХДТО = ПолучитьОбъектXDTO(ФайлОписания.ПолноеИмя, ФабрикаПакета);
		Если ОбъектХДТО = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		СтруктураМакета = Новый Структура;

		#Удаление
		Если ТипЗнч(ОбъектХДТО.Template.Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
			Попытка
				Синоним = ОбъектХДТО.Template.Properties.Synonym.item.content;
			Исключение
				Синоним = "";
			КонецПопытки;
		Иначе
			Синоним = "";
			Для каждого ЭлементСписка Из ОбъектХДТО.Template.Properties.Synonym.item Цикл
				Если ЭлементСписка.lang = "ru" Тогда
					Синоним = ЭлементСписка.content;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		#КонецУдаления
		#Вставка
		Синоним = СинонимОбъекта(ОбъектХДТО.Template);
		#КонецВставки

		Имя     = ОбъектХДТО.Template.Properties.Name;
		СтруктураМакета.Вставить("Синоним"      , Синоним);
		СтруктураМакета.Вставить("Идентификатор", ОбъектХДТО.Template.uuid);

		СтруктураМакета.Вставить("TemplateType" , ОбъектХДТО.Template.Properties.TemplateType);


		// Модуль объекта
		ИмяФайлаШаблона = ФайлОписания.ИмяБезРасширения
		+ ".Template";

		СтруктураМакета.Вставить("ХэшМодуля", ПолучитьХэшСуммуМакетаИзОдногоКаталога(ИмяФайлаШаблона));

		СтруктураМакетов.Вставить(Имя, СтруктураМакета);

	КонецЦикла;

	Если СтруктураМакетов.Количество() > 0 Тогда
		СтруктураСвойств.Вставить("Templates", СтруктураМакетов);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ЕстьСиноним(ОбъектХДТО)

	Возврат ОбъектХДТО.Properties.Synonym.Свойства().Количество()
	
КонецФункции

&НаКлиенте
Функция СинонимОбъекта(ОбъектХДТО)
	
	Если Не ЕстьСиноним(ОбъектХДТО) Тогда 
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(ОбъектХДТО.Properties.Synonym.item) = Тип("ОбъектXDTO") Тогда
		Возврат ОбъектХДТО.Properties.Synonym.item.content;
	Иначе
		Для каждого ЭлементСписка Из ОбъектХДТО.Properties.Synonym.item Цикл
			Если ЭлементСписка.lang = "ru" Тогда
				Возврат ЭлементСписка.content;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат "";
		
КонецФункции

#КонецОбласти