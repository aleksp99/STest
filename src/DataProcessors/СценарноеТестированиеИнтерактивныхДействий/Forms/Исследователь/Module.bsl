#Область ОбработчикиСобытийФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы // <ИмяТаблицыФормы>

&НаКлиенте
Процедура PA_ДеревоОбъектовПриАктивизацииСтрокиПосле(Элемент)
	
	Если Элементы.ДеревоОбъектовАвтоТекущий.Пометка Тогда 
		ВыполнитьКоманду("Активизировать");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура PA_ДеревоОбъектовВыборВместо(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыполнитьКоманду("Нажать", Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьКоманду(ИмяКоманды, ОбновитьДеревоОбъектов = Ложь)
	
	ОчиститьСообщения();
	
	// см. ВыполнитьМетодДляТекущегоОбъекта()
	ТекущиеДанныеДерева = Элементы.ДеревоОбъектов.ТекущиеДанные;
	Если ТекущиеДанныеДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТаблицаМетодов.НайтиСтроки(Новый Структура("Метод", ИмяКоманды)).Количество() Тогда 
		Возврат;
	КонецЕсли;
	
	// Получение объекта
	ИмяОбъекта       = ТекущиеДанныеДерева.ИмяОбъекта;
	ЗаголовокОбъекта = ТекущиеДанныеДерева.ЗаголовокОбъекта;
	ТипОбъекта       = ТекущиеДанныеДерева.ТипОбъекта;
	Если Лев(ИмяОбъекта, 1) = "<" ИЛИ ПустаяСтрока(ИмяОбъекта) Тогда
		ИмяОбъекта = Неопределено;
	КонецЕсли;
	
	Если ТипОбъекта = "ТестируемоеПриложение" Тогда
		НайденныйОбъект = СцТ_ГлавнаяФорма.СцТ_Контроллер;
	Иначе
		ТестОбъект = СцТ_ГлавнаяФорма.СцТ_Контроллер;
		Для каждого  ЭлементПути Из ТекущиеДанныеДерева.ПутьКОбъекту.МассивРодителей Цикл
			
			Если ЭлементПути.ТипОбъекта = "ТестируемоеПриложение" Тогда
				Продолжить;
			ИначеЕсли ЭлементПути.ТипОбъекта = "ТестируемыйКомандныйИнтерфейсОкна" Тогда
				НайденныйОбъект = ТестОбъект.ПолучитьКомандныйИнтерфейс();
				Если НайденныйОбъект = Неопределено Тогда
					Возврат;
				КонецЕсли;
				
				ТестОбъект = НайденныйОбъект;
			Иначе
				ЗаголовокЭлементапути = ЭлементПути.ЗаголовокОбъекта;
				ИмяЭлементапути       = ЭлементПути.ИмяОбъекта;
				Если Лев(ИмяЭлементапути, 1) = "<" ИЛИ ПустаяСтрока(ИмяЭлементапути) Тогда
					ИмяЭлементапути = Неопределено;
				КонецЕсли;
				
				НайденныйОбъект = ТестОбъект.НайтиОбъект(Тип(ЭлементПути.ТипОбъекта), ЗаголовокЭлементапути, ИмяЭлементапути, 4);
				Если НайденныйОбъект = Неопределено Тогда
					Возврат;
				КонецЕсли;
				
				ТестОбъект = НайденныйОбъект;
			КонецЕсли;
			
		КонецЦикла;
		
		
		НайденныйОбъект = ТестОбъект.НайтиОбъект(Тип(ТипОбъекта), ЗаголовокОбъекта, ИмяОбъекта, 4);
	КонецЕсли;
	
	ТестируемыйОбъект = НайденныйОбъект;
	Результат = Неопределено;
	
	// Выполнение и расшифровка результата
	ОписаниеРезультата = "";
	
	Попытка
		Выполнить("ТестируемыйОбъект." + ИмяКоманды + "()");
	Исключение
		ТексОшибки = НСтр("ru = 'При выполнении метода ""%1"" возникла ошибка.'");
		ТексОшибки = СтрЗаменить(ТексОшибки, "%1", ИмяКоманды);
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Описание = ИнформацияОбОшибке.Описание;
		Причина  = ИнформацияОбОшибке.Причина;
		
		Если НЕ ПустаяСтрока(Описание) Тогда
			ТексОшибки = ТексОшибки + Символы.ПС + НСтр("ru = 'Описание:'") + " " + Описание;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Причина) Тогда
			ТексОшибки = ТексОшибки + Символы.ПС + НСтр("ru = 'Причина:'") + " " + Причина;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТексОшибки);
		
	КонецПопытки;
	
	Если Не ОбновитьДеревоОбъектов Тогда 
		Возврат;
	КонецЕсли;
	
	МассивРодителей = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ТекущиеДанныеДерева.ПутьКОбъекту.МассивРодителей);
	
	СтруктураПоследнегоРодителя = Новый Структура;
	СтруктураПоследнегоРодителя.Вставить("ТипОбъекта"      , ТипОбъекта);
	СтруктураПоследнегоРодителя.Вставить("ИмяОбъекта"      , Строка(ИмяОбъекта));
	СтруктураПоследнегоРодителя.Вставить("ЗаголовокОбъекта", ЗаголовокОбъекта);
	
	Обновить(Элементы.Обновить);
	
	НайтиТекущийЭлементВДереве(ДеревоОбъектов, СтруктураПоследнегоРодителя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
&Вместо("ПолучитьСвойстваОбъекта")
Функция PA_ПолучитьСвойстваОбъекта(РодительскийОбъект)
	
	Возврат ?(ТипЗнч(РодительскийОбъект) = Тип("Структура"), РодительскийОбъект, ПродолжитьВызов(РодительскийОбъект));
	
КонецФункции

#КонецОбласти